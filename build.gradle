plugins {
    id 'java'
    id 'org.springframework.boot' version '4.0.0-M2'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'org.graalvm.buildtools.native' version '0.10.3'
    id 'io.freefair.lombok' version '8.10'
}

group = 'com.jac'
version = '0.0.1-SNAPSHOT'
description = 'JacMS Service'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/milestone' }
}

ext {
    mapstructVersion = '1.5.5.Final'
    jsonwebtokenVersion = '0.13.0'
}

dependencies {

    // -- CORE
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    // -- Data Base
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb-reactive'

    // -- Utilidades
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    implementation "org.mapstruct:mapstruct:${mapstructVersion}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"

    implementation "io.jsonwebtoken:jjwt-api:${jsonwebtokenVersion}"
    runtimeOnly "io.jsonwebtoken:jjwt-impl:${jsonwebtokenVersion}"
    runtimeOnly "io.jsonwebtoken:jjwt-jackson:${jsonwebtokenVersion}"

    implementation 'com.github.ben-manes.caffeine:caffeine:3.2.3'


    // AÃ±ade Log4j2
    implementation 'org.springframework.boot:spring-boot-starter-log4j2'

    // -- Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'

    configurations.configureEach {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.annotationProcessorPath = configurations.annotationProcessor
    options.compilerArgs += ['-Amapstruct.defaultComponentModel=spring']
}

tasks.named('test') {
    useJUnitPlatform()
}

springBoot {
    buildInfo()
}

graalvmNative {
    binaries {
        main {
            imageName.set('jac-ms')
            useFatJar.set(true)
            fallback.set(true)
        }
    }
}
